apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'android-maven'

group = 'com.moovel'

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifactoryPublish {
    dependsOn sourceJar
}

publishing {
    publications {
        sdk(MavenPublication) {
            groupId group
            artifactId project.name
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            artifact sourceJar
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'http://localhost:8081/artifactory/'
    publish {
        repository {
            repoKey = version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
            username = project.hasProperty('artifactoryUser') ? project.artifactoryUser : System.getenv('ARTIFACTORY_USER')
            password = project.hasProperty('artifactoryPass') ? project.artifactoryPass : System.getenv('ARTIFACTORY_PASSWORD')
            //snapshot = true
        }
        defaults {
            publishArtifacts = true
            publications('sdk')
            publishPom = true
            publishIvy = false
        }
    }
}